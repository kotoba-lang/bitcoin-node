(ns bitcoin.node.disk-utxo-test
  (:require [bitcoin.node.consensus-test :as fixture]
            [bitcoin.node.disk-utxo :as disk]
            [clojure.test :refer [deftest is]])
  (:import [java.nio.file Files Path]
           [java.nio.file.attribute FileAttribute]))

(defn- with-store [run!]
  (let [directory
        (Files/createTempDirectory
         "bitcoin-node-utxo-" (make-array FileAttribute 0))
        path (.resolve directory "chainstate.sqlite")]
    (try
      (run! path)
      (finally
        (doseq [suffix ["-shm" "-wal" ""]]
          (Files/deleteIfExists
           (Path/of (str path suffix) (make-array String 0))))
        (Files/deleteIfExists directory)))))

(deftest raw-mainnet-blocks-connect-reopen-and-disconnect
  (with-store
    (fn [path]
      (let [host (disk/open {:path path :network :mainnet})]
        (is (= 1
               (:coin-count
                (disk/connect-block! host (fixture/hex->bytes fixture/genesis)))))
        (is (= 2
               (:coin-count
                (disk/connect-block! host
                                     (fixture/hex->bytes fixture/block-one)))))
        (let [reopened (disk/open {:path path :network :mainnet})]
          (is (= 1 (:height (disk/status reopened))))
          (is (= :ok (:integrity (disk/integrity-check! reopened))))
          (is (= 1 (:coin-count (disk/disconnect-tip! reopened))))
          (is (= 0 (:height (disk/status reopened)))))))))

(deftest unsupported-network-fails-before-creating-storage
  (is (= :bitcoin.node/unsupported-consensus-network
         (:type
          (ex-data
           (try
             (disk/open {:path "unused.sqlite" :network :signet})
             (catch clojure.lang.ExceptionInfo error error)))))))
